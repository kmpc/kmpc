# どこぞのログオンバッチ解説

**以下は基本的に管理者権限があるユーザーを想定する。**
諸々のファイルは，例のGoogleDriveに置いておくのでそれを使って。
## タスクバー・スタートメニューのレイアウトを変更する
タスクバー・スタートメニューに置きたい**ショートカットのファイル**を`%APPDATA%`以下にコピーする。

他のユーザーに影響を与えないために，ショートカットファイルの名前をユニークなものにする。

ex) `Notepad_00000.lnk`
```bat
copy "H:\_data\aviutl.lnk" "%APPDATA%\Microsoft\Windows\Start Menu\Programs"
copy "H:\_data\aviutl (2).lnk" "%APPDATA%\Microsoft\Windows\Start Menu\Programs"
copy "H:\_data\hoge.lnk" "%APPDATA%\Microsoft\Windows\Start Menu\Programs"
copy "H:\_data\Notepad_00000.lnk" "%APPDATA%\Microsoft\Windows\Start Menu\Programs"
copy "H:\_data\Google Chrome_00000.lnk" "%APPDATA%\Microsoft\Windows\Start Menu\Programs"
```

Powershell を使用して，レイアウトを適用する。

レイアウトのConfigファイルである`H:\_data\config\StartMenu_20191112.xml`はあらかじめ作成しておく。
```bat
powershell -command "Import-startlayout -layoutpath H:\_data\config\StartMenu_20191112.xml -mountpath c:\ "
```

レイアウトのキャッシュを削除する。

`<ランダム文字列>`はマシン固有の文字列？のよう。変更されることはなかった。Powershell でワイルドカードを使ってやるのがいちばんな気がする。
```bat
REG delete HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\CloudStore\Store\Cache\DefaultAccount\$de${<ランダム文字列>}$start.tilegrid$windows.data.curatedtilecollection.tilecollection /f
```

Powershell を使用して，エクスプローラーを再起動する。このときデスクトップ画面が一瞬ちらつく(暗転)するので，バッチ内のはじめのほうに行う。

→レイアウトの変更が適用される(configファイルを指定してコマンド打っただけでは反映されないので)。
```bat
powershell -command "Get-Process Explorer | Stop-Process"
```

## 壁紙を変更する
管理者権限がない場合は，`wallpaper.exe`で壁紙を変更するしか方法がない。

管理者権限がある場合は，レジストリを書き換えることでも壁紙を変更することが可能。
```bat
if "%COMPUTERNAME%"=="hoge1" (
    REM "H:\hoge\wallpaper.exe" H:\hoge\fuga.jpg
    "H:\hoge\random_image.bat"
) else if "%COMPUTERNAME%"=="hoge2" (
    REM "H:\hoge\wallpaper.exe" "H:\hoge\15.jpg"
    "H:\hoge\random_image.bat"
) else (
    "H:\hoge\wallpaper.exe" H:\hoge\fugagufa.jpg
)
```

## Unity + VSCode の際のコード補完を有効にする
(@ Unity 2018.4.11f1 (64-bit))

.NET Framework 4.7.1 の DevPack をインストールすることで，コード補完が機能するようになる。

英語版，日本語版の両方をインストールしておく。
```bat
"H:\_data\installer\NDP471-DevPack-ENU.exe" /s /v"/qn" REBOOT=ReallySuppress
"H:\_data\installer\NDP471-DevPack-JPN.exe" /s /v"/qn" REBOOT=ReallySuppress
```

## Unity のユーザープロファイルを再起動後でも引き継げるようにする
ネットワークドライブに，各マシン用のディレクトリを作成しておく。ユーザーログオン時に，そのディレクトリとローカルとのシンボリックリンクを作成する。シンボリックリンクを使用してUnity側を騙す。
基本的に，各マシンを使う人は固定あることが前提。`H:\_data\machine\%COMPUTERNAME%\`をコピーすることで他のマシンでも同じプロファイルを引き継げる。

また，これを利用すればUnity以外のソフトでも再起動後もプロファイルを引き継ぐことが可能(だと思う)。


とりあえず，ネットワークドライブに`%COMPUTERNAME%`を含むディレクトリを作成しておく。
```bat
md "H:\_data\machine\%COMPUTERNAME%\AppData\Local"
md "H:\_data\machine\%COMPUTERNAME%\AppData\LocalLow"
md "H:\_data\machine\%COMPUTERNAME%\AppData\Roaming"
```

AppData に [AppdData\Unity] <-> [ネットワークドライブ] のシンボリックリンクを作成
```bat
mklink /d "C:\Users\%USERNAME%\AppData\Local\Unity" "H:\_data\machine\%COMPUTERNAME%\AppData\Local\Unity"
mklink /d "C:\Users\%USERNAME%\AppData\LocalLow\Unity" "H:\_data\machine\%COMPUTERNAME%\AppData\LocalLow\Unity"
mklink /d "C:\Users\%USERNAME%\AppData\Roaming\Unity" "H:\_data\machine\%COMPUTERNAME%\AppData\Roaming\Unity"
```

## その他いろいろ
特定のユーザーがログオンした場合にのみ実行されるように`if`にした。

- GitHubDesktop をインストール
- IME の 「a あ」を非表示
- 隠しファイル属性付きファイルを表示

```bat
if %USERNAME%==hoge (
    REM GitHubDesktop インストーラー サイレンス 再起動なし
    "H:\_data\installer\GitHubDesktopSetup.exe" /s /v"/qn" REBOOT=ReallySuppress
    REM IME 「a あ」 の表示をしない
    REG ADD "HKEY_CURRENT_USER\SOFTWARE\Microsoft\IME\15.0\IMEJP\MSIME" /v ShowImeModeNotification /t REG_DWORD /d 0 /f
    REM 隠しファイル属性付きを表示
    REG ADD "HKEY_CURRENT_USER\SOFTWARE\Microsoft\Windows\CurrentVersion\Explorer\Advanced" /v "Hidden" /t REG_DWORD /d "1" /f
)
```
